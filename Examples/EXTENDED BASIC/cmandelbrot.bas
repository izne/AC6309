1 ACIA = &HA001
2 GOSUB 3001 : REM *** CLEAR THE SCREEN ***
3 REM *** SIZE IS FOR TERMINAL OF COLxROW - 189x51 (INTIGER DEVIDE EACH BY 2)***
10 FOR Y=-20 TO 20
20 FOR X=-40 TO 40
25 REM *** MULTIPLY EACH TERM BY 2 TO SHRINK, DIVIDE TO ENLARGE ***
30 CA=X*0.0229
40 CB= Y*0.04166
50 A=CA
60 B=CB
70 FOR I=0 TO 15
80 T=A*A-B*B+CA
90 B=2*A*B+CB
100 A=T
110 IF (A*A+B*B)>4 THEN GOTO 200
120 NEXT I
130 POKE ACIA, 32
140 GOTO 210
200 IF I>9 THEN I=I+7
202 CD=48+I
205 GOSUB 2000 : POKE ACIA, CD
210 NEXT X
220 POKE ACIA, 13: POKE ACIA, 10
230 NEXT Y
235 END
2000 IF CD = LCD THEN GOTO 2010 : REM ** COLOR HAS NOT CHANGED ***
2001 POKE ACIA, 27:POKE ACIA, 91 : REM *** '<esc>[' folowing selects color ***
2002 IF CD = 32 THEN POKE ACIA, 51:POKE ACIA, 55
2003 IF CD >= 33 AND CD < 42 THEN POKE ACIA, 51:POKE ACIA, 49
2004 IF CD >= 42 AND CD < 51 THEN POKE ACIA, 51:POKE ACIA, 50
2005 IF CD >= 51 AND CD < 60 THEN POKE ACIA, 51:POKE ACIA, 51
2006 IF CD = 60 THEN POKE ACIA, 51:POKE ACIA, 52
2007 IF CD >= 61 OR CD = 62 THEN POKE ACIA, 51:POKE ACIA, 53
2009 POKE ACIA, 109 : REM *** 'm' (finishes control code for color)
2010 REM *** ENDSUB ***
2011 LCD = CD
2020 RETURN
3000 DATA 27,91,50,74 : REM *** '<esc>[2J' / Clears the Screen
3001 FOR A = 1 TO 4:READ SENDBYTE:POKE ACIA,SENDBYTE:NEXT A
3002 RETURN